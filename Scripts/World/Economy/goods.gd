class_name Goods
extends Resource

@export var wood : int = 0
@export var iron : int = 0
@export var ruby : int = 0

func _init(new_wood: int = 0, new_iron : int = 0, new_ruby : int = 0):
	wood = new_wood
	iron = new_iron
	ruby = new_ruby


func has_enough(needed : Goods):
	return  wood >= needed.wood and \
			iron >= needed.iron and \
			ruby >= needed.ruby


func subtract(cost : Goods):
	wood -= cost.wood
	iron -= cost.iron
	ruby -= cost.ruby


func add(resource : Goods):
	wood += resource.wood
	iron += resource.iron
	ruby += resource.ruby


func clear():
	wood = 0
	iron = 0
	ruby = 0

func to_string_short(empty: String = "") -> String:
	if wood == 0 and iron == 0 and ruby == 0:
		return empty
	var result = ""
	if wood != 0:
		result += "%d ðŸª“" % wood
	if iron != 0:
		if result != "":
			result += " | "
		result += "%d â›ï¸" % iron
	if ruby != 0:
		if result != "":
			result += " | "
		result += "%d ðŸ’Ž" % ruby
	return result

func _to_string() -> String:
	return "%d ðŸª“| %d â›ï¸| %d ðŸ’Ž" % to_array()

func to_array() -> Array[int]:
	return [wood, iron, ruby]


static func from_array(array : Array) -> Goods:
	var wood = (array[0] if 0 in range(array.size()) else 0) as int
	var iron = (array[1] if 1 in range(array.size()) else 0) as int
	var ruby = (array[2] if 2 in range(array.size()) else 0) as int
	return Goods.new(wood, iron, ruby)
